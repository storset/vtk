resourcetype shared-text {

  properties {
    shared-text-box : json (title : string, id : string, description-en : html,description-no : html,description-nn : html) multiple noextract,
    link-other-language: resource_ref
  }

  edit-rules {
    shared-text-box (class[vrtx-json-accordion]),
    description-en (size[small]),
    description-nn (size[small]),
    description-no (size[small]),
    group descriptions (description-en, description-no, description-nn) (after id) horizontal
  }

  view-components {    

    shared-text-box {
      ##
      [def document structured-document()]
      [def boxes document.properties.shared-text-box]
      [if boxes != null]
        
        [list boxes box]
          [def title box.title]
          [def description-en box.description-en]
          [def description-no box.description-no]
          [def description-nn box.description-nn]
          [def id box.id]
             
          [def number _index + 1]
          <div class="vrtx-shared-text-title">
            [if title != null && title != ""]
              <h2>[val title # unescaped]</h2>
            [endif]
          </div>

          [if id != null && id != ""]
            <div class="vrtx-shared-text-box" id="[val id]">
            (ID: [val id])
          [else]
            <div class="vrtx-shared-text-box">
          [endif]
             
            <div class="vrtx-shared-text-description">
              [if description-en != null && description-en != ""]
                [val description-en # unescaped]
              [endif]
            </div>
             
            <div class="vrtx-shared-text-description">
              [if description-no != null && description-no != ""]
                [val description-no # unescaped]
              [endif]
            </div>
             
            <div class="vrtx-shared-text-description">
              [if description-nn != null && description-nn != ""]
                [val description-nn # unescaped]
              [endif]
            </div>
               
          </div>
       [endlist]
    [endif]
    ##
    }
    
    link-other-language {
      ##
      [def link resource-prop(".", "link-other-language")]
      [if link != null]
        <a id="vrtx-change-language-link" href="[val link]">[localized "link-language"]</a>
      [endif]
      ##
    }
    
  }
  
  view {
    ##
    <!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Transitional//EN" "http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd">
    <html xmlns="http://www.w3.org/1999/xhtml">
      <head>
        <title></title>
      </head>
      <body id="vrtx-shared-text">
        <div id="vrtx-content">
          <div id="vrtx-main-content">
            [call "comp:link-other-language"]
            [call "comp:shared-text-box"]
          </div>
        </div>
      </body>
    </html>
    ##
  }

  localization {
     title : (en : "Title", no : "Tittel", nn : "Tittel"),
     id : (en : "Identifier", no : "Identifikator", nn : "Identifikator"),
     shared-text-box : (en : "Content", no : "Innhold", nn : "Innhold"),
     boxTitle : (en : "Title", no : "Tittel", nn : "Tittel"),
     boxType : (en : "Type", no : "Type", nn : "Type"),
     description-en : (en : "English", no : "Engelsk", nn : "Engelsk"),
     description-no : (en : "Norwegian Bokmål", no : "Bokmål", nn : "Bokmål"),
     description-nn : (en : "Norwegian Nynorsk", no : "Nynorsk", nn : "Nynorsk"),
     boxUrl : (en : "Webaddress", no : "Webadresse", nn : "Webadresse"),
     header : (en : "Edit shared text", no : "Rediger fellestekst", nn : "Rediger fellestekst"),
     link-language: (en : "Norwegian", no : "English", nn : "English"),
     link-other-language {
       en: (en : "Link to norwegian version", no : "Link to english version", nn : "Link to english version"),
       no: (en : "Lenke til norsk versjon", no : "Lenke til engelsk versjon", nn : "Lenke til engelsk versjon"),
       nn: (en : "Lenke til norsk versjon", no : "Lenke til engelsk versjon", nn : "Lenke til engelsk versjon")
     }
  }
}