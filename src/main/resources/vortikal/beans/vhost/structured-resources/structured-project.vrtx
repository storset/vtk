resourcetype structured-project {

  properties {
    name : string,
    title : derived (name) eval (name) overrides title,
    status-ongoing : boolean,
    introduction : simple_html,
    picture : image_ref,
    pictureAlt: string,
    caption : simple_html,
    hidePicture : boolean defaultvalue("false"),
    content : html noextract,
    tags : string multiple overrides tags,
    contactInfo : html noextract,
    participantsUsernames : string multiple,
    participantsCachedFullNames : binary,
    participants : json (participantName:string, participantUrl:string) multiple,
    numberOfParticipantsToDisplay : string defaultvalue("50"),
    relatedGroups : resource_ref multiple,
    rssFeeds : json (rssTitle:string, rssUrl:string) multiple,
    related-content : html noextract,
    link-other-language: resource_ref
  }
  
  edit-rules {
    participantsUsernames (class[vrtx-autocomplete-username]),
    caption (size[small]),
    numberOfParticipantsToDisplay (dropdown),
    group introImageAndCaption (picture, caption, hidePicture, pictureAlt) (after introduction),
    tags tooltip (en : "(Tag1, Tag2, ...)", no : "(Emneord1, Emneord2, ...)", nn : "(Emneord1, Emneord2, ...)"),
    tags (class[vrtx-autocomplete-tag])
  }

  scripts {
    participantsUsernames multipleinputfields,
    relatedGroups multipleinputfields
  }
    
  services {
    participantsUsernames participantsFullNameService affects(participantsCachedFullNames)
  }

  view-components {
  
    introduction {
      ##
      [def intro resource-prop(".", "introduction")]
      [if intro != null]
        <div class="vrtx-introduction">[val intro # unescaped]</div>
      [endif]
      ##
    }
  
    completed {
      ##
      [def document structured-document()]
      [def ongoing document.properties.status-ongoing]
      [if (ongoing = "false")]
        ([localized "completed"])
      [endif]
      ##
    }
   
   related-content-in-additional-column {
      ##
      [def document structured-document()]
      [def theRelated-content document.properties.related-content]
      [if theRelated-content != null]
        <div id="vrtx-related-content">
          [val theRelated-content # unescaped]
        </div>
      [endif]
      ##
    }
    
    introduction-image {
      ##
      [def img-uri resource-prop(".", "picture")]
      [def pictureAlt resource-prop(".", "pictureAlt")]
      [def caption resource-prop(".", "caption")]
      [def pixel-width null]
      [def img null]
      [def pixel-width null]
      [def photographer null]
      [def style null]
      
      [if img-uri != null]
        [def img resource(img-uri)]
      [endif]
      [if img != null]
        [def pixel-width resource-prop(img-uri, "pixelWidth")]
        [def photographer resource-prop(img-uri, "photographer")]
      [endif]
      [if pixel-width != null]
        [def style concat("width:", pixel-width, "px")]
      [endif]
     
      [if img-uri != null]
        [if pixel-width != null]
          <div class="vrtx-introduction-image" style="[val style]">
        [else]
          <div class="vrtx-introduction-image">
        [endif]
        
        <img src="[val img-uri]" alt="[if pictureAlt != null && pictureAlt != ""][val pictureAlt][endif]" />

        [if caption != null]
          <div class="vrtx-imagetext">
            <div class="vrtx-imagedescription">[val caption # unescaped]</div>
            [if photographer != null]
              <span class="vrtx-photo">
                <span class="vrtx-photo-prefix">[localized "photo-prefix"]: </span>[val photographer]
              </span>
            [endif]
          </div>
        [elseif photographer != null]
          <div class="vrtx-imagetext">
            [if photographer != null]
              <span class="vrtx-photo">
                <span class="vrtx-photo-prefix">[localized "photo-prefix"]: </span>[val photographer]
              </span>
            [endif]
          </div>
        [endif]
        </div>
      [endif]
      ##
    }

    contact-info {
      ##
      [resource-props "."]
      [def document structured-document()]
      [def contactinfo document.properties.contactInfo]
      [if contactinfo != null]
        <div class="vrtx-project-contactinfo vrtx-frontpage-box">
          <h2>[localized "contactInfoView"]</h2>
          [val contactinfo # unescaped]
        </div>
      [endif]
      ##
    }
    
    feeds {
      ##
      [resource-props "."]
      [if rssFeeds != null]
        [def document structured-document()]
        [def feeds document.properties.rssFeeds]
        [if feeds != null]
          [list feeds feed]
            [def thetitle feed.rssTitle]
            [def theurl feed.rssUrl]
              
            [if theurl != null && theurl != ""]
              [if thetitle != null && thetitle != ""]
                [call "include:feed"  {"url":theurl, "override-feed-title":thetitle, "feed-title":true, "max-messages":3, "include-if-empty":false}]
              [else]
                [call "include:feed" {"url":theurl, "feed-title":true, "max-messages":3, "include-if-empty":false}]
              [endif]
            [endif]
          [endlist]
        [endif]
      [endif]
      ##
    }
    
    vrtx-date-info{
      ##
      [resource-props "."]
      [if publish-date != null]
        <div class="vrtx-date-info">
        <span class="published-date-label">[localized "publishedDate"]</span> <span class="published-date">[val publish-date] </span>
        [if publish-date < lastModified]
          - <span class="last-modified-date">[localized "lastModifiedDate"]</span> <span class="last-modified-date">[val lastModified]</span>
        [endif]
        </div>
      [endif]
      ##
    }
    
    link-other-language {
      ##
      [def link resource-prop(".", "link-other-language")]
      [if link != null]
        <a id="vrtx-change-language-link" href="[val link]">[localized "link-language"]</a>
      [endif]
      ##
    }
  }

  view {
    ##
    <!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Transitional//EN" "http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd">
    <html xmlns="http://www.w3.org/1999/xhtml">
      <head>
        <title>[call "json:field" {"select":"name"}] [call "comp:completed"]</title>
      </head>
      <body id="vrtx-structured-project">
        <div id="vrtx-content">
          [def document structured-document()]
          <div id="vrtx-main-content">
            [call "comp:link-other-language"]
            <h1>[call "json:field" {"select":"name"}] [call "comp:completed"]</h1>
            [call "comp:introduction"]
            [def theHidePicture document.properties.hidePicture]
            [if theHidePicture = "false" || theHidePicture = null]
              [call "comp:introduction-image"]
            [endif]
            [call "json:field" {"select": "content"}]
            [call "resource:tags"]
            [call "comp:vrtx-date-info"]
          </div>
          <div id="vrtx-additional-content">
            [call "comp:contact-info"]
            [call "resource:person-list"]
            [call "resource:groups-related-to-project"]
            [call "comp:feeds"]
            [call "comp:related-content-in-additional-column"]
          </div>
        </div>
      </body>
    </html>
    ##
  }
  
  vocabulary {
    status-ongoing {
      en : ("true" = "Ongoing", "false" = "Completed"),
      no : ("true" = "Pågående", "false" = "Avsluttet"),
      nn : ("true" = "Pågående", "false" = "Avslutta")
    },
    numberOfParticipantsToDisplay {
      en : (range = "0..25,30,35,40,45,50"),
      no : (range = "0..25,30,35,40,45,50"),
      nn : (range = "0..25,30,35,40,45,50")
    },
    hidePicture {
      en : ("true" = "Yes", "false" = "No"),
      no : ("true" = "Ja", "false" = "Nei"),
      nn : ("true" = "Ja", "false" = "Nei")
    }
  }
  
  localization {
    name : (en : "Name", no : "Navn", nn : "Navn"),
    introduction : (en : "Introduction",no : "Innledning", nn : "Innleiing"),
    status-ongoing : (en : "Status", no : "Status", nn : "Status"),
    picture : (en: "Image", no: "Bilde", nn : "Bilete"),
    introduction-image-alt : (en: "Image", en: "Bilde", en: "Bilete"),
    caption : (en : "Image caption", no : "Bildetekst", nn : "Bilettekst"),
    photo-prefix : (en : "Photo", no : "Foto", nn : "Photo"),
    hidePicture : (en : "Hide image in project", no : "Skjul bilde i prosjekt", nn : "Skjul bilete i prosjekt"),
    related-content : (en : "Related content (right column)", no : "Relatert innhold (vises i høyre kolonne)", nn : "Relatert innhald (visast i høgre kolonne)"),
    participantsHeader : (en : "Participants", no : "Deltakere", nn : "Deltakarar"),
    participantsUsernames : (en : "Participants from UiO (enter the participant username)",no : "Deltakere fra UiO (fyll inn deltakerens brukernavn)", nn :"Deltakarar fra UiO (fyll inn deltakarens brukernavn)"),
    participants : (en : "Other participants", no : "Andre deltakere", nn : "Andre deltakarar"),
    participantName : (en : "Name",no : "Navn", nn :"Namn"),
    participantUrl : (en : "Webaddress",no : "Webadresse", nn :"Webadresse"),
    relatedGroups : (en : "Involved research groups", no : "Involverte forskergrupper", nn : "Involverte forskargrupper"),
    rssFeeds : (en : "Subscribe to RSS-feeds",no : "Abonner på RSS-strømmer", nn :"Abonner på RSS-strømmar"),
    rssTitle : (en : "Title",no : "Tittel", nn : "Tittel"),
    rssUrl : (en : "Webaddress",no : "Webadresse", nn :"Webadresse"),
    tags : (en : "Tags", no : "Emneord", nn : "Emneord"),
    content : (en : "Content", no :"Innhold",nn : "Innhold"),
    contactInfo : (en : "Contact information", no : "Kontaktinformasjon", nn : "Kontaktinformasjon"),
    contactInfoView : (en : "Contact", no : "Kontakt", nn : "Kontakt"),
    header : (en : "Edit project", no : "Rediger prosjekt", nn : "Rediger prosjekt"),
    publishedDate : (en : "Published", no :"Publisert", nn:"Publisert"),
    lastModifiedDate : (en : "Last modified", no : "Sist endret", nn : "Sist endra"),
      numberOfParticipantsToDisplay : (
      en : "Number of participants to show",
      no : "Antall deltakere som skal vises",
      nn : "Antall deltakarar som skal visast"),
    completed : (en : "completed", no : "avsluttet", nn : "avslutta"),
    link-language: (en : "Norwegian", no : "English", nn : "English"),
    link-other-language {
      en: (en : "Link to norwegian version", no : "Link to english version", nn : "Link to english version"),
      no: (en : "Lenke til norsk versjon", no : "Lenke til engelsk versjon", nn : "Lenke til engelsk versjon"),
      nn: (en : "Lenke til norsk versjon", no : "Lenke til engelsk versjon", nn : "Lenke til engelsk versjon")
    }
  }
}